plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

version = prj_version

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven {
        name = "hytale-release"
        url = uri("https://maven.hytale.com/release")
    }
    maven {
        name = "hytale-pre-release"
        url = uri("https://maven.hytale.com/pre-release")
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    compileOnly("com.hypixel.hytale:Server:2026.01.22-6f8bdbdc4")
}

test {
    useJUnitPlatform()
}

tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)

        // All the fields in `manifest.json` are set here.
        manifestJson.Group = prj_group
        manifestJson.Name = prj_name
        manifestJson.Version = prj_version
        manifestJson.Description = prj_description
        manifestJson.Website = prj_website
        manifestJson.Main = entry_point

        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

tasks.named('jar') {
    dependsOn 'processResources'
}

tasks.register('deployPlugin', Copy) {
    dependsOn jar
    from jar.archiveFile

    // XXX: This was a workaround for an issue where `/pl r` does not reload the plugin due to
    //      the plugin being loaded in memory. This doesn't reliably work either, avoid.
    // Generate random name once per execution
    //    def randomName = UUID.randomUUID().toString()
    //
    //    from(jar.archiveFile) {
    //        rename { String originalName ->
    //            // Preserve extension (.jar) just in case
    //            "${randomName}.jar"
    //        }
    //    }

    into "$hytaleHome/UserData/Mods"
    doLast { println "Plugin deployed to Hytale Mods folder." }
}

def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}


idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --disable-sentry --assets=\"$hytale_home_dir/install/$hytale_patchline/package/game/$hytale_game_build/Assets.zip\" --mods=${file("src/main/").absolutePath} --auth-mode=authenticated"
        workingDirectory = serverRunDir.absolutePath
    }
}